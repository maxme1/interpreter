program = statement*

# TODO: add empty statements consisting just of a semicolon
statement = (StmtBody DELIMITER) | block | ifStmt | whileStmt | funcDef

StmtBody = expression | 'break' | 'continue' | returnStmt
returnStmt = 'return' expression?

# TODO: possibly optional first statement
ifStmt = 'if' '(' expression ')' (statement ('else' statement)? )?
whileStmt = 'while' '(' expression ')' statement
block = '{' statement* '}'

funcDef = 'def' IDENTIFIER '(' argsList? ')' block
argsList = IDENTIFIER (SEPARATOR IDENTIFIER)*

-- expression --

expression = comparison | assignment

assignment = IDENTIFIER '=' expression

comparison = term (( '==' | '>=' | '<=' | '<' | '>' | '!=' ) term )*
term = factor ( ('+' | '-') factor )*
factor = unary ( ('*' | '/') unary )*
# TODO: maybe add unlimited unary operations
unary = ('+' | '-')? primary

primary = literal ( '(' call? ')' )*
literal = NUMBER | '(' expression ')' | IDENTIFIER | BOOL | NONE

call = expression (SEPARATOR expression)*

-- literals --

NUMBER = '\d+'
IDENTIFIER = '[^a-zA-Z_]\w*'
BOOL = 'True' | 'False'
NONE = 'None'
DELIMITER = ';'
SEPARATOR = ','