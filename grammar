program = statement*

statement = (StmtBody? DELIMITER) | ifStmt | whileStmt | tryStmt | funcDef | classDef

StmtBody = expression | 'break' | 'continue' | returnStmt | raiseStmt | importStmt
returnStmt = 'return' expression?
raiseStmt = 'raise' expression?

importStmt = 'import' IDENTIFIER
tryStmt = 'try' argsValues? block ('catch' argsValues block)*
# TODO: possibly optional first statement
ifStmt = 'if' '(' expression ')' (statement ('else' statement)? )?
whileStmt = 'while' '(' expression ')' statement
block = '{' statement* '}'

funcDef = 'def' IDENTIFIER arguments? block
arguments = '(' argsList? ')'
argsList = IDENTIFIER (SEPARATOR IDENTIFIER)*

classDef = 'class' IDENTIFIER superclass? block
superclass = EXTENDS expression

-- expression --

expression = comparison | assignment

assignment = assignable | IDENTIFIER '=' expression
assignable = primary attribute | item

comparison = term (( '==' | '>=' | '<=' | '<' | '>' | '!=' ) term )*
term = factor ( ('+' | '-') factor )*
factor = unary ( ('*' | '/') unary )*
# TODO: maybe add unlimited unary operations
unary = ('+' | '-')? primary

primary = literal suffix*
literal = NUMBER | '(' expression ')' | IDENTIFIER | BOOL | NONE | STRING

suffix = call | attribute | item
attribute = '.' IDENTIFIER
item = '[' expression ']'
call = '(' argsValues? ')'
argsValues = expression (SEPARATOR expression)*

-- base definitions --

NUMBER = '\d+'
STRING = "'[^']*'"
BOOL = 'True' | 'False'
NONE = 'None'
IDENTIFIER = '[a-zA-Z_]\w*'
DELIMITER = '[;\n]'
SEPARATOR = ','
EXTENDS = 'extends'
COMMENT = '#[^\n]*'